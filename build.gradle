plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java.srcDirs = ['src']
    }
    test {
        java.srcDirs = ['tests']
        resources.srcDirs = ['test-resources']
    }
    example {
        java.srcDirs = ['examples']
        resources.srcDirs = ['example-resources']
    }
}

dependencies {
    implementation files('lib/HermiT.jar')
    implementation files('lib/py4j-0.10.9.7.jar')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    exampleImplementation files('lib/HermiT.jar')
    exampleImplementation sourceSets.main.output
}

processResources {
    from('config.properties')
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
    useJUnitPlatform()
}

application {
    mainClass = 'gateway.JavaGateway'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

shadowJar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    mergeServiceFiles()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Test).configureEach {
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.register('startGatewayServer', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'gateway.JavaGateway'
}